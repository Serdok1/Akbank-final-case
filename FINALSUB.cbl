       IDENTIFICATION DIVISION.
       PROGRAM-ID. FINALSUB.
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT IDX-FILE ASSIGN TO IDXFILE
                             ORGANIZATION INDEXED
                             ACCESS RANDOM
                             RECORD KEY IDX-KEY
                             STATUS ST-IDX-FILE.
       DATA DIVISION.
       FILE SECTION.
       FD  IDX-FILE.
         01  IDX-REC.
           05 IDX-KEY.
              10 IDX-ID              PIC S9(05) COMP-3.
              10 IDX-DVZ             PIC S9(03) COMP.
           05 IDX-NAME               PIC X(15).
           05 IDX-SURNAME            PIC X(15).
           05 IDX-DATE               PIC S9(07) COMP-3.
           05 IDX-BALANCE            PIC S9(15) COMP-3.

       WORKING-STORAGE SECTION.
         01  WS-WORK-AREA.
           05 ST-IDX-FILE            PIC 9(02).
              88 IDX-SUCCESS                   VALUE 00 97.
           05 WS-INT-DATE            PIC 9(07).
           05 WS-GREG-DATE           PIC 9(08).
           05 WS-NEW-NAME            PIC X(15) VALUE 'S I N A        '.
           05 WS-NEW-SURNAME         PIC X(15) VALUE 'OZBAYRAM       '.
           05 WS-READ-VALID          PIC 9(01).
           05 WS-OPEN-VALID          PIC 9(01) VALUE 0.
           05 WS-INDEX-I             PIC 9(15).
           05 WS-INDEX-J             PIC 9(15).

      * BURADAKI DEGISKENLERI ANA PROGRAM ILE BIRLIKTE ORTAK BIR SEKILDE
      * KULLANABILMEK ICIN LINKAGE SECTION ALTINDA TANIMLIYORUZ.
      * AYRICA BURADAKI DEGISKENLER ANA PROGRAMDA, ISIMLERI HARIC
      * TAMAMEN AYNI OLMAK ZORUNDA 
        LINKAGE SECTION.
         01 LS-SUB-AREA.
           05 LS-SUB-FUNC            PIC 9(01).
              88 LS-FUNC-OPEN                VALUE 1.
              88 LS-FUNC-READ                VALUE 2.
              88 LS-FUNC-UPDATE              VALUE 3.
              88 LS-FUNC-WRITE               VALUE 4.
              88 LS-FUNC-DELETE              VALUE 8.
              88 LS-FUNC-CLOSE               VALUE 9.
           05 LS-SUB-ID              PIC 9(05).
           05 LS-SUB-DVZ             PIC 9(03).
           05 LS-SUB-RC              PIC 9(02).
           05 LS-SUB-DATA.
              10 LS-SUB-NAME-TO           PIC X(15).
              10 LS-SUB-NAME-FROM         PIC X(15).
              10 LS-SUB-SURNAME-TO        PIC X(15).
              10 LS-SUB-SURNAME-FROM      PIC X(15).
              10 LS-SUB-DATE              PIC 9(08).
              10 LS-SUB-BALANCE           PIC 9(15).
              10 LS-SUB-COMM              PIC X(50).
      *--------------------
      * ANA PROGRAM ILE ORTAK KULLANDIGIMIZ DEGISKENLERI BURADA USING
      * KULLANARAK BELIRTIYORUZ. BU SAYEDE ANA PROGRAMDA YAPILAN BIR
      * DEGISIKLIK BURADA, BURADA YAPILAN BIR DEGISIKLIK DE
      * ANA PROGRAMDA ETKILI OLACAK.
       PROCEDURE DIVISION USING LS-SUB-AREA.
       0000-MAIN.
      * LS-SUB-FUN DEGISKENINI DEGERLENDIRIP HANGI ALT DEGISKEN
      * TRUE ISE ISTENILEN ISLEMI YAPIYORUZ
           EVALUATE LS-SUB-FUNC
               WHEN 1
                  PERFORM H110-FUNC-OPEN
               WHEN 2
                  PERFORM H120-FUNC-READ
                WHEN 3
                  PERFORM H130-FUNC-UPDATE
                WHEN 4
                  PERFORM H140-FUNC-WRITE
                WHEN 8
                  PERFORM H150-FUNC-DELETE
                WHEN 9
                  PERFORM H300-FUNC-CLOSE
           END-EVALUATE.
       0000-END. EXIT.

       H110-FUNC-OPEN.
      * DAHA ONCEDEN DOSYANIN ACILIP ACILMADIGINI KONTROL EDIYORUZ
           IF NOT WS-OPEN-VALID = 1
      * ACMA ISLEMINI YAPACAGIMIZ KISIMI CALISTIRIYORUZ
            PERFORM H200-OPEN-FILE
            IF WS-OPEN-VALID = 1
      * ISLEMDEN SONRA DOSYA BASARILI BIR SEKILDE ACILDIYSA
      * RC VE ACIKLAMAYI DAHA SONRA CIKTIDA KULLANACAGIMIZ
      * DEGISKENLERE ATIYORUZ
             MOVE 'FILE OPENED SUCCESSFULLY.' TO LS-SUB-COMM
             MOVE  ST-IDX-FILE TO LS-SUB-RC
            ELSE
             MOVE 'ERROR OCCURRED WHEN OPENING THE FILE.' TO LS-SUB-COMM
             MOVE  ST-IDX-FILE TO LS-SUB-RC
           ELSE
            MOVE 'FILE ALREADY OPEN.' TO LS-SUB-COMM
           END-IF.
      * ANA PROGRAMA GERI DONMEK ICIN KULLANMAMIZ GEREKEN KOD
           GOBACK.
       H110-END. EXIT.

       H120-FUNC-READ.
      * DAHA ONCEDEN DOSYANIN ACILIP ACILMADIGINI KONTROL EDIYORUZ
           IF NOT WS-OPEN-VALID = 1
            PERFORM H200-OPEN-FILE
            IF NOT WS-OPEN-VALID = 1
      * DOSYANIN ACILDIGI SIRADA BIR HATA MEYDANA GELIRE
      * BUNU ILGILI DEGISKENLERE YAZIYORUZ
             MOVE 'ERROR OCCURRED WHILE OPENING THE FILE FOR READ.'
      -        TO LS-SUB-COMM
             MOVE  ST-IDX-FILE TO LS-SUB-RC
           END-IF.
      * OKUMA ISLEMINI YAPACAGIMIZ KISIMI CALISTIRIYORUZ
           PERFORM H210-READ-FILE.
           IF WS-READ-VALID = 1
      * DONEN VERIYE GORE KAYIT BULUNUP BULUNAMADIGINI ISLIYORUZ
             MOVE 'FILE READ SUCCESSFULLY COMPLETED.' TO LS-SUB-COMM
             MOVE  ST-IDX-FILE TO LS-SUB-RC
           ELSE
             MOVE 'NO RECORD FOUND IN FILE.' TO LS-SUB-COMM
             MOVE  ST-IDX-FILE TO LS-SUB-RC
           END-IF.
      * ANA PROGRAMA GERI DONMEK ICIN KULLANMAMIZ GEREKEN KOD
           GOBACK.
       H120-END. EXIT.

       H130-FUNC-UPDATE.
      * DAHA ONCEDEN DOSYANIN ACILIP ACILMADIGINI KONTROL EDIYORUZ
           IF NOT WS-OPEN-VALID = 1
            PERFORM H200-OPEN-FILE
            IF NOT WS-OPEN-VALID = 1
             MOVE 'ERROR OCCURRED WHILE OPENING THE FILE FOR UPDATE.'
      -        TO LS-SUB-COMM
             MOVE  ST-IDX-FILE TO LS-SUB-RC
           END-IF.
            PERFORM H210-READ-FILE.
            IF WS-READ-VALID = 1
      * BUTUN KONTROLLER YAPILDIKTAN SONRA 3 FARKLI ISLEM YAPIYORUZ
      * ILK OLARAK ISMI GUNCELLEMEMIZ GEREKLI
             PERFORM H131-UPDATE-NAME
      * SONRASINDA SOYISMI GUNCELLIYORUZ
             PERFORM H132-UPDATE-SURNAME
      * SON OLARAK VSAM DOSYASINI TEKRAR YAZIP ISIMDEKI BOSLUKLARIN
      * KONTROLUNU YAPIYORUZ
             PERFORM H133-REWRITE-CONTROL
            ELSE
      * EGER VSAMDA BULUNMAYAN BIR KAYITA GUNCELLEME ISTENIRSE
      * BOYLE BIR KAYDIN OLMADIGINI BILDIRIYORUZ
             MOVE 'NO RECORD FOUND IN FILE.' TO LS-SUB-COMM
             MOVE  ST-IDX-FILE TO LS-SUB-RC
            END-IF.
      * ANA PROGRAMA GERI DONMEK ICIN KULLANMAMIZ GEREKEN KOD
            GOBACK.
       H130-END. EXIT.

       H131-UPDATE-NAME.
      * DONGUDE KULLANACAGIMIZ INDEKS VERILERINI SIFIRLIYORUZ
           COMPUTE WS-INDEX-I = 0
           COMPUTE WS-INDEX-J = 0
      * GUNCELLEME YAPMADAN ONCEKI ISIM VERISINI NAME-FROM DEGISKENINE
      * ATIYORUZ
           MOVE IDX-NAME TO LS-SUB-NAME-FROM
      * BASIT BIR DONGU KULLANARAK IDX-NAME'IN ICERISINDE BOSLUK
      * HARICINDEK HER SEYI NAME-TO DEGISKENINE ATIYORUZ
      * BOYLECE ISIMIN BOSLUKSUZ HALINI ELDE EDIYORUZ
           PERFORM UNTIL WS-INDEX-I > LENGTH OF IDX-NAME
            IF IDX-NAME(WS-INDEX-I:1) NOT = SPACE
               MOVE IDX-NAME(WS-INDEX-I:1)
      -         TO LS-SUB-NAME-TO(WS-INDEX-J:1)
               ADD 1 TO WS-INDEX-J
            END-IF
            ADD 1 TO WS-INDEX-I
           END-PERFORM.
      * VSAM ICERISINDEKI ISIMI DEGISTIRIYORUZ FAKAT HENUZ REWRITE
      * YAPMADIGIMIZ ICIN VSAM UZERINDE KALICI BIR DEGISIKLIK OLMUYOR
           MOVE LS-SUB-NAME-TO TO IDX-NAME.
       H131-END. EXIT.

       H132-UPDATE-SURNAME.
      * SOYISMIN ASIL HALINI SURNAME-FROM DEGISKENINE ATIYORUZ
           MOVE IDX-SURNAME TO LS-SUB-SURNAME-FROM
      * INSPECT KULLANARAK IDX-SURNAME ICERISINDEKI BUTUN E HARFLERINI
      * I, BUTUN A HARFLERINI DE E YAPIYORUZ
      * INSPECT FONKSIYONU REWRITE ISLEMINI KENDI ICERISINDE YAPTIGI
      * ICIN BU DEGISIKLIK VSAM UZERINDE KALICI OLUYOR
           INSPECT IDX-SURNAME REPLACING ALL 'E' BY 'I'
           INSPECT IDX-SURNAME REPLACING ALL 'A' BY 'E'
           MOVE IDX-SURNAME TO LS-SUB-SURNAME-TO.
       H132-END. EXIT.

       H133-REWRITE-CONTROL.
      * YAPILAN BUTUN DEGISIKLIKLERIN VSAM UZERINDE KALICI OLMASI ICIN
      * REWRITE KULLANIYORUZ
           REWRITE IDX-REC
      * GUNCEL ISMIN ESKI ISIME ESIT OLMASI DURUMUNDA, ESKI ISIMDE
      * BOSLUK KULLANILMAMIS DEMEKTIR. BUNA GORE CIKTILARI AYARLIYORUZ
           IF LS-SUB-NAME-FROM = LS-SUB-NAME-TO
             MOVE 'THERE IS NO SPACES IN THE NAME.' TO LS-SUB-COMM
           ELSE
             MOVE 'UPDATE SUCCESSFULLY COMPLETED.' TO LS-SUB-COMM
           END-IF.
       H133-END. EXIT.

       H140-FUNC-WRITE.
      * DAHA ONCEDEN DOSYANIN ACILIP ACILMADIGINI KONTROL EDIYORUZ
           IF NOT WS-OPEN-VALID = 1
            PERFORM H200-OPEN-FILE
            IF NOT WS-OPEN-VALID = 1
             MOVE 'ERROR OCCURRED WHILE OPENING THE FILE FOR WRITING.'
      -        TO LS-SUB-COMM
             MOVE  ST-IDX-FILE TO LS-SUB-RC
           END-IF.
      * DOSYAYI YAZMADAN ONCE OKUMA ISLEMINI YAPIP ISTENILEN KAYDI
      * BULMAMIZ GEREKLI. BUNUN ICIN OKUMA ISLEMINI YAPIYORUZ
            PERFORM H210-READ-FILE.
      * EGER KAYIT BULUNDUYSA YAZMA ISLEMLERINE BASLIYORUZ
            IF WS-READ-VALID = 1
             MOVE IDX-ID TO LS-SUB-ID
             MOVE IDX-DVZ TO LS-SUB-DVZ
             MOVE ST-IDX-FILE TO LS-SUB-RC
             MOVE IDX-NAME TO LS-SUB-NAME-FROM
             MOVE WS-NEW-NAME TO LS-SUB-NAME-TO
             MOVE WS-NEW-NAME TO IDX-NAME
             MOVE IDX-SURNAME TO LS-SUB-SURNAME-FROM
             MOVE WS-NEW-SURNAME TO LS-SUB-SURNAME-TO
             MOVE WS-NEW-SURNAME TO IDX-SURNAME
             MOVE IDX-DATE TO LS-SUB-DATE
             MOVE IDX-BALANCE TO LS-SUB-BALANCE
      * YAPTIGIMIZ DEGISIKLIKLERIN VSAM UZERINDE KALICI OLMASI ICIN
      * REWRITE KOMUTUNU KULLANIYORUZ
             REWRITE IDX-REC
             MOVE 'WRITE SUCCESSFULLY COMPLETED.' TO LS-SUB-COMM
            ELSE
      * ISTENILEN KAYIT BULUNAMADIYSA ILGILI CIKTIYI OLUSTURUYORUZ
             MOVE 'NO RECORD FOUND IN FILE.' TO LS-SUB-COMM
             MOVE  ST-IDX-FILE TO LS-SUB-RC
            END-IF.
      * ANA PROGRAMA GERI DONMEK ICIN KULLANMAMIZ GEREKEN KOD
            GOBACK.
       H140-END. EXIT.

       H150-FUNC-DELETE.
      * DAHA ONCEDEN DOSYANIN ACILIP ACILMADIGINI KONTROL EDIYORUZ
           IF NOT WS-OPEN-VALID = 1
            PERFORM H200-OPEN-FILE
            IF NOT WS-OPEN-VALID = 1
             MOVE 'ERROR OCCURRED WHILE OPENING THE FILE FOR DELETE.'
      -        TO LS-SUB-COMM
             MOVE  ST-IDX-FILE TO LS-SUB-RC
           END-IF.
      * SILINECEK KAYDI BULMAK ICIN OKUMA ISLEMI YAPIYORUZ
            PERFORM H210-READ-FILE.
            IF WS-READ-VALID = 1
      * EGER KAYIT BULUNDUYSA DELETE KOMUTU ILE ILGILI KAYDI SILIYORUZ
      * DELETE KOMUTU KENDI ICERISINDE REWRITE OZELLIGINI KULLANDIGI
      * ICIN YAPILAN DEGISIKLIK VSAM UZERINDE KALICI OLUYOR
             DELETE IDX-FILE
             MOVE 'RECORD SUCCESSFULLY DELETED.'
      -        TO LS-SUB-COMM
            END-IF.
      * ANA PROGRAMA GERI DONMEK ICIN KULLANMAMIZ GEREKEN KOD
            GOBACK.
       H150-END. EXIT.

       H200-OPEN-FILE.
           DISPLAY 'OPENING...'
      * IDX-FILE HEM OKUNUP HEM DE ICERISINDE DEGISIKLIK YAPILACAGI ICIN
      * INPUT-OUTPUT OLARAK CIFT YONLU ACIYORUZ
           OPEN I-O  IDX-FILE.
           IF IDX-SUCCESS
      * DOSYANIN BASARILI BIR SEKILDE ACILIP ACILMADIGININ KONTROLU
             COMPUTE WS-OPEN-VALID = 1
           END-IF.
       H200-END. EXIT.

       H210-READ-FILE.
      * DOSYA DAHA ONCE OKUNDUYSA KONTROL DEGISKENI OLAN WS-READ-VALID
      * DEGERI 1 OLUYOR VE BU DURUMDA DOSYA TEKRARDAN OKUNURKEN BASARILI
      * BIR SEKILDA OKUNMAZSA WS-READ-VALID DEGERI SIFIR DONUYOR
      * EGER BU ISLEMI YAPMASAYDIK TEK BIR BASARILI OKUMA SONRASINDA
      * BUTUN OKUMALAR BASARILI SAYILIRDI
           COMPUTE WS-READ-VALID = 0.
      * ANAHTAR OLARAK KULLANACAGIMIZ DEGISKENLERI ANAHTAR DEGERLERE
      * ATIYORUZ
           MOVE LS-SUB-ID TO IDX-ID.
           MOVE LS-SUB-DVZ TO IDX-DVZ.
           READ IDX-FILE KEY IS IDX-KEY
      * ANAHTARI KULLANARAK KAYIT ARIYORUZ. KAYIT BULUNMASI DURUMUNDA
      * HANGI ISLEMI YAPMAK ISTEDIGIMIZI NOT VALID KISMINDA BELIRTIYORUZ
           INVALID KEY PERFORM H220-INVALIDKEY
           NOT INVALID KEY PERFORM H230-VALIDREC.
       H210-END. EXIT.

       H220-INVALIDKEY.
      * BURADA HERHANGI BIR SEY YAPMAMIZA GEREK YOK CUNKU
      * KONTROL DEGISKENI H210-READ-FILE'IN EN BASINDA SIFIRLANIYOR
           DISPLAY 'INVALID KEY :' IDX-ID IDX-DVZ.
       H220-END. EXIT.

       H230-VALIDREC.
      * KAYDIN BULUNMASI HALINDE BUTUN KAYIT VERILERINI
      * DAHA SONRA YAZDIRMAK ICIN KULLANACAGIMIZ DEGISKENLERE ATIYORUZ
           MOVE IDX-ID TO LS-SUB-ID
           MOVE IDX-DVZ TO LS-SUB-DVZ
           MOVE ST-IDX-FILE TO LS-SUB-RC.
           MOVE IDX-NAME TO LS-SUB-NAME-FROM
           MOVE SPACES TO LS-SUB-NAME-TO
           MOVE IDX-SURNAME TO LS-SUB-SURNAME-FROM
           MOVE SPACES TO LS-SUB-SURNAME-TO
           MOVE IDX-DATE TO LS-SUB-DATE
           MOVE IDX-BALANCE TO LS-SUB-BALANCE
      * KAYDIN BASARILI BIR SEKILDE BULUNDUGUNU BELIRTMEK ICIN KONTROL
      * DEGISKENINI 1 YAPIYORUZ
           COMPUTE WS-READ-VALID = 1.
       H230-END. EXIT.
      *
       H300-FUNC-CLOSE.
      * IDX-FILE'I KAPATIYORUZ AYRICA, DOSYANIN DAHA ONCEDEN ACILIP
      * ACILMADIGINI GOSTEREN KONTROL DEGISKENINI 0 YAPIYORUZ YANI
      * BIR SONRAKI DOSYA OKUMA ISLEMINDE DOSYANIN KAPALI OLDUGUNU
      * YAZDIGIMIZ FONKSIYONLAR BILECEK
           CLOSE IDX-FILE.
           COMPUTE WS-OPEN-VALID = 0.
           MOVE ST-IDX-FILE TO LS-SUB-RC.
           MOVE 'FILE SUCCESSFULLY CLOSED.' TO LS-SUB-COMM.
      * ANA PROGRAMA GERI DONMEK ICIN KULLANMAMIZ GEREKEN KOD
           GOBACK.
       H300-END. EXIT.
